buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.12.64'
    classpath 'com.moowork.gradle:gradle-node-plugin:0.8'
    classpath 'be.filipblondeel.gradle:gradle-gulp-plugin:0.1'
    classpath 'org.codehaus.groovy:groovy-backports-compat23:2.3.5'
  }
}

group = "com.netflix.spinnaker.deck"

apply plugin: 'spinnaker-gradle-project'
apply plugin: 'spinnaker-debpublish'
apply plugin: 'com.moowork.node'

jar {
  dependsOn('webpack')
  from(fileTree("build/webpack")) {
    into "META-INF/resources"
  }
}

node {
    // Version of node to use.
    version = '0.12.3'
    npmVersion = '2.8.3'

    // Enabled the automatic download. False is the default (for now).
    download = true
}

task webpack(type: Exec) {
  workingDir '.'
  commandLine 'node', 'node_modules/webpack/bin/webpack.js'
  environment = [
    "GATE_HOST": "spinnaker-api-prestaging.prod.netflix.net",
  ]
}

webpack.outputs.dir file('build/webpack')

webpack.dependsOn 'npmInstall'
buildDeb.dependsOn 'webpack'

ospackage {
  packageName = "deck"
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/var/www"
  requires "apache2"
  from "build/webpack"
}
